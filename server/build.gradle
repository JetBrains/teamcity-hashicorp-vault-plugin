/*
 * Copyright 2000-2020 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'io.github.rodm.teamcity-server'
apply plugin: 'io.github.rodm.teamcity-environments'

ext {
    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
}

dependencies {
    implementation(project(':common')) {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation("org.springframework.vault:spring-vault-core:$sprint_vault_version") {
        exclude group: 'org.springframework'
    }

    compileOnly "org.jetbrains.teamcity.internal:web:$teamcity_version"
    compileOnly "org.jetbrains.teamcity.internal:server:$teamcity_version"
    compileOnly "org.jetbrains.teamcity:oauth:$teamcity_version"

    agent project(path: ':agent', configuration: 'plugin')

    testImplementation(testFixtures(project(':common')))
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation "org.jetbrains.teamcity:oauth:$teamcity_version"
}

teamcity {
    version = teamcity_version
    allowSnapshotVersions = true

    server {
        descriptor {
            name = 'teamcity-vault-support'
            displayName = 'HashiCorp Vault Support'
            description = 'Provides support for HashiCorp Vault Secrets Manager'
            version = project.version

            vendorName = 'JetBrains'
            vendorUrl = 'https://www.jetbrains.com'

            useSeparateClassloader = true
            nodeResponsibilitiesAware = true
        }
        archiveName = "${project.rootProject.name}.zip"
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        teamcity2018_1 {
            version = '2018.1.5'
            serverOptions = "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5600 -DTC.res.disableAll=true -Dteamcity.development.mode=true"
            agentOptions = "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5601"
        }
    }
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions { jvmTarget = '1.8' }
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}


// Do not include version into plugin archive name
project.tasks.getByName('serverPlugin').version = ''


