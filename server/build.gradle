/*
 * Copyright 2000-2020 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java'
apply plugin: 'kotlin'

apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'com.github.rodm.teamcity-environments'

ext {
    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
}

dependencies {
    compile(project(':common')) {
        exclude group: 'org.apache.httpcomponents'
    }

    provided "org.jetbrains.teamcity.internal:web:$teamcity_version"
    provided "org.jetbrains.teamcity:oauth:$teamcity_version"

    agent project(path: ':agent', configuration: 'plugin')

    testImplementation(testFixtures(project(':common')))
}

teamcity {
    version = teamcity_version

    server {
        descriptor {
            name = 'teamcity-vault-support'
            displayName = 'HashiCorp Vault Support'
            description = 'Provides support for HashiCorp Vault Secrets Manager'
            version = project.version

            vendorName = 'JetBrains'
            vendorUrl = 'https://www.jetbrains.com'

            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        teamcity2018_1 {
            version = '2018.1.5'
            serverOptions = "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5600 -DTC.res.disableAll=true -Dteamcity.development.mode=true"
            agentOptions = "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5601"
        }
    }
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions { jvmTarget = '1.8' }
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}


// Do not include version into plugin archive name
project.tasks.getByName('serverPlugin').version = ''

// Fix after https://github.com/rodm/gradle-teamcity-plugin/commit/ca6d5dd4946f670a30cf39f837ae3aea831ef9eb
project.tasks.getByName('serverPlugin').baseName = "${project.rootProject.name}"

