/*
 * Copyright 2000-2020 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '1.4.1'
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version
    }
}

ext {
    version_prefix = '0.1'
    teamcity_version = '2020.1'
    // compile
    spring_version = '5.3.18'
    sprint_vault_version = '2.3.2'
    httpclient_version = '4.3.6' // version is relevant to one used inside TeamCity
    // tests
    jetty_version = '9.4.19.v20190610'
    testcontainers_version = '1.15.2'
    assertj_version = '1.7.1'
}

group = 'org.jetbrains.teamcity.vault'
version = "${version_prefix}-SNAPSHOT"

if (System.getenv('BUILD_NUMBER') != null) {
    version = "${System.env.BUILD_NUMBER}"
}


subprojects {
    group = rootProject.group
    version = rootProject.version

    apply plugin: 'java'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'kotlin'

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions { jvmTarget = '1.8' }
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    dependencies {
        compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}")
    }
    if (System.getenv('TEAMCITY_VERSION') != null) {
        test.ignoreFailures = true
    }
}

wrapper {
    gradleVersion = '6.9.3'
    distributionUrl = "https://cache-redirector.jetbrains.com/services.gradle.org/distributions/gradle-${gradleVersion}-bin.zip"
}